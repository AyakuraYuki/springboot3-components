plugins {
  id 'java-platform'
  alias(libs.plugins.moe.karla.maven.publishing)
  id 'maven-publish'
}

description = 'Spring Boot 3 Components'

defaultTasks 'build'

repositories {
  mavenCentral()
}

allprojects {
  tasks.register('showArtifact', () -> { println("artifact: ${project.group}:${project.name}:${project.version}") })
}

subprojects {
  apply plugin: 'java-library'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    implementation platform(libs.netty.bom)
    implementation platform(libs.spring.bom)
    implementation platform(libs.jackson.bom)

    implementation(libs.slf4j.api)
    implementation(libs.jakarta.annotation)

    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
    testCompileOnly(libs.lombok)
    testAnnotationProcessor(libs.lombok)

    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
  }

  tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
  }
  compileJava.options.encoding = 'UTF-8'
  compileTestJava.options.encoding = 'UTF-8'

  test {
    useJUnitPlatform()
  }
}

mavenPublishing {
  publishingType = moe.karla.maven.publishing.MavenPublishingExtension.PublishingType.USER_MANAGED
  url = 'https://github.com/AyakuraYuki/springboot3-components'
  developer('Ayakura Yuki', 'AyakuraYuki@users.noreply.github.com')
}

publishing {
  publications {
    main(MavenPublication) {
      from(project.components.java)
    }
  }
}
