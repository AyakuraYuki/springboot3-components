import com.vanniktech.maven.publish.SonatypeHost

plugins {
  id 'java-platform'
  alias(libs.plugins.maven.publisher)
}

description = 'Spring Boot 3 Components'

defaultTasks 'build'

repositories {
  mavenCentral()
}

allprojects {
  tasks.register('showArtifact', () -> { println("artifact: ${project.group}:${project.name}:${project.version}") })
}

subprojects {
  apply plugin: 'java-library'

  java {
    // withSourcesJar()
    // withJavadocJar()
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    implementation platform(libs.netty.bom)
    implementation platform(libs.spring.bom)
    implementation platform(libs.jackson.bom)

    implementation(libs.slf4j.api)
    implementation(libs.jakarta.annotation)

    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
    testCompileOnly(libs.lombok)
    testAnnotationProcessor(libs.lombok)

    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
  }

  tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
  }
  compileJava.options.encoding = 'UTF-8'
  compileTestJava.options.encoding = 'UTF-8'

  test {
    useJUnitPlatform()
  }
}

mavenPublishing {
  configureBasedOnAppliedPlugins(true, true)
  publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
  signAllPublications()

  coordinates(group as String, name as String, version as String)

  pom {
    name = project.name
    description = project.description
    inceptionYear = '2025'
    url = 'https://github.com/AyakuraYuki/springboot3-components'
    licenses {
      license {
        name = 'MIT License'
        url = 'https://github.com/AyakuraYuki/springboot3-components/blob/main/LICENSE'
      }
    }
    developers {
      developer {
        id = 'AyakuraYuki'
        name = 'Ayakura Yuki'
        url = 'https://github.com/AyakuraYuki'
      }
    }
    scm {
      url = 'https://github.com/AyakuraYuki/springboot3-components'
      connection = 'scm:git:https://github.com/AyakuraYuki/springboot3-components.git'
      developerConnection = 'scm:git:https://github.com/AyakuraYuki/springboot3-components.git'
    }
  }
}
